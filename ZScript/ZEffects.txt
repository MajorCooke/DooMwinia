Class DarwiniaExplosion : Actor
{
	double sw;
	Default
	{
		+NOINTERACTION
		+NOTONAUTOMAP
		+BRIGHT
		RenderStyle "Add";
		Scale 0;
	}
	States
	{
	Spawn:
		X051 A 0
		{
			A_SetRenderStyle(1,STYLE_Add);
			A_Stop();
			bNOINTERACTION = true;
		}
		"####" "#####" 1
		{
			sw += (90.0/5.0);
			A_SetScale(sin(sw) * 1.2);
		}
		"####" "#" 0
		{
			int rng = random(15,25);
			for (int i = 0; i < rng; i++)
			{
				A_SpawnItemEx("DarwiniaExplosionParticleRed",
					frandom(0.0,15.0), 0, frandom(-15.0, 15.0),
					frandom(-5.0, 5.0),0,frandom(3.0,9.0),random(0,359),SXF_NOCHECKPOSITION);
			}
		}
		"####" "#" 1
		{
			if (sw >= 180) { return Destroy(); }
			sw += (90.0/12.0);
			A_SetScale(sin(sw) * 1.2);
		}
		Wait;
	}
}

Class DarwiniaExplosionParticleRed : Actor
{
	int ticker;
	int tmax;
	Default
	{
		+NOINTERACTION
		+THRUACTORS
		+ROLLSPRITE
		+BRIGHT
		RenderStyle "Add";
		Alpha 0.99;
		Translation "0:255=%[0,0,0]:[1.0,0.25,0.25]";
	}
	
	override void PostBeginPlay()
	{
		tmax = random(35,70);
		roll = 45;
		Scale.X = Scale.Y = frandom(2.25,2.75);
		Super.PostBeginPlay();
	}
	
	override void Tick()
	{
		if (globalfreeze || level.frozen)
			return Super.Tick();
		
		ticker++;
		if (!(ticker % randompick(6,7,7,8,8,8)) && pos.z > floorz)
			A_SpawnItemEx("DarwiniaExplosionParticleSmoke",0,0,0,0,0,-1.0,0,SXF_NOCHECKPOSITION);
		vel.z -= 0.2;
		if (ticker > tmax)
			A_FadeOut(0.02);
		Super.Tick();
	}
	
	States
	{
	Spawn:
		M100 D -1;
		Stop;
	}
}

Class DarwiniaExplosionParticleSmoke : Actor
{
	int ticker;
	Default
	{
		+NOINTERACTION
		+THRUACTORS
		+ROLLSPRITE
		+BRIGHT
		RenderStyle "Translucent";
		Alpha 0.99;
		//Translation "0:255=%[0,0,0]:[1.0,0.25,0.25]";
	}
	
	override void PostBeginPlay()
	{
		roll = 45;
		Scale.X = Scale.Y = frandom(1.25,1.75);
		Super.PostBeginPlay();
	}
	
	override void Tick()
	{
		if (globalfreeze || level.frozen)	return Super.Tick();
			
		ticker++;
		A_FadeOut(0.02);
		Super.Tick();
	}
	
	States
	{
	Spawn:
		M100 D -1;
		Stop;
	}
}