class MyGlobalVariables : Thinker
{
	int testVar;

	MyGlobalVariables Init()
	{
		ChangeStatNum(STAT_INFO);
		return self;
	}

	MyGlobalVariables Add(int i)
	{
		testVar += i;
		return self;
	}

	static MyGlobalVariables Get()
	{
		ThinkerIterator it = ThinkerIterator.Create("MyGlobalVariables",STAT_INFO);
		let p = MyGlobalVariables(it.Next());
		if (p == null)
		{
			p = new("MyGlobalVariables").Init();
		}
		return p;
	}
}


//==============================================================================
//
//
//
//==============================================================================



Class DarwinianSide : Actor
{
	Default
	{
		+ROLLSPRITE
		+FLATSPRITE
		+NOINTERACTION
		+MASKROTATION
		+ABSMASKPITCH
		VisibleAngles -11.25, 11.25;
		VisiblePitch -90.0, 90.0;
	}
	States
	{
	Spawn:
		M100 B -1;
		Stop;
	}
}

Class DarwinianBack : DarwinianSide
{
	Default
	{
		VisibleAngles 101.25, 348.75;
		VisiblePitch -90.0, 90.0;
	}
	States
	{
	Spawn:
		M100 A -1;
		Stop;
	}
}
Class DarwinianLimb : Actor
{
	int rollamt;
	int angamt;
	int pitamt;
	Default
	{
		Projectile;
		+PAINLESS
		+ROLLSPRITE
		+FLATSPRITE
		+ROLLCENTER
		-NOGRAVITY
		BounceType "Doom";
		Scale 0.4;
		BounceFactor 1.0;
		WallBounceFactor 1.0;
		Gravity 0.25;
		RenderStyle "Translucent";
		Height 32;
		Radius 32;
	}
	
	override bool CanCollideWith(Actor other, bool passive)
	{
		if (!passive)
		{
			if (other && other is "DWBuilding")
			{
				BounceMe();
				return true;
			}
			return false;
		}
		return true;
	}
	
	override void PostBeginPlay()
	{
		Scale.X = frandom(0.4,0.5);
		Scale.Y = frandom(0.4,0.5);
		rollamt = frandom(3.0,20.0) * randompick(-1,1);
		angamt = frandom(3.0,20.0) * randompick(-1,1);
		pitamt = frandom(3.0,20.0) * randompick(-1,1);
		Super.PostBeginPlay();
	}
	
	override void Tick()
	{
		A_SetRoll(roll + rollamt);
		A_SetAngle(angle + angamt);
		A_SetPitch(pitch + pitamt);
		Super.Tick();
	}
	
	void BounceMe()
	{
		rollamt = frandom(3.0,20.0) * randompick(-1,1);
		angamt = frandom(3.0,20.0) * randompick(-1,1);
		pitamt = frandom(3.0,20.0) * randompick(-1,1);
	}
	
	States
	{
	Spawn:
		M100 K random(35,70);
		M100 K 1 A_FadeOut(1.0/45.0);
		Wait;
	}
}