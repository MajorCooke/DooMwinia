extend class DarwinianPlayer
{
	bool Damaged;
	//==========================================================================
	override int DamageMobj(Actor inflictor, Actor source, int damage, Name mod, int flags = 0, double angle = 0)
	{
		damage = Min(damage, TELEFRAG_DAMAGE - 1);
		int dmg = damage, realdmg = damage;
		int ch = player.cheats;
		
		if (GameOver) //Kill it and don't respawn.
		{
			// Override all other flags.
			// You win NOTHING. YOU LOSE! GOOD DAY SIR.
			if (player) player.cheats &= ~(CF_BUDDHA|CF_BUDDHA2|CF_GODMODE|CF_GODMODE2);
			bBUDDHA = bINVULNERABLE = bNODAMAGE = false;
			flags = DMG_FORCED|DMG_THRUSTLESS;
			damage = health * 10;
		}
		else
		{
			bBUDDHA = true;
			if (player && ((ch & CF_GODMODE2) || (damage < TELEFRAG_DAMAGE && (ch & CF_GODMODE))))
			{
				return 0;
			}
			// There's a reason for this. The player must absolutely not die until the game is over.
			flags &= ~DMG_FORCED;
		}
		
		// Filter out damage modifiers and make sure it's not cancelled first.
		if (!(flags & DMG_NO_PROTECT))
		{
			flags |= DMG_NO_PROTECT;
			dmg = GetModifiedDamage(mod, dmg, true);
		}
		if (!(flags & DMG_NO_FACTOR))
		{
			flags |= DMG_NO_FACTOR;
			dmg = ApplyDamageFactor(mod, dmg);
		}
		Actor src = (inflictor == null) ? source : inflictor;
		//bool isThrustless = (!src || src.bNODAMAGETHRUST || flags & DMG_THRUSTLESS);
		//flags |= DMG_THRUSTLESS;
		bFLOORHUGGER = false;
		dmg = Super.DamageMobj(inflictor, source, dmg, mod, flags, angle);
		Damaged = (!GameOver && dmg > 0);
		bFLOORHUGGER = (vel.z == 0.0 && A_CheckFloor("Spawn"));
		
		
		/*
		if (Damaged && src && !isThrustless)
		{
			double dmd = dmg * 0.125;
			if (dmd >= 1.0)
			{
				double tempAngle = angle;
				double tempPitch = pitch;
				
				Vector3 vec = Vec3To(src);
				double srcang = deltaangle(angle, AngleTo(src));
				double 
				
				A_Face(src, 0, 0, 0, 0, FAF_MIDDLE);
				angle += 180;
				pitch *= -1.0;
				A_ChangeVelocity(cos(pitch) * dmd, 0, -sin(pitch) * dmd, CVF_RELATIVE);
				angle = tempAngle;
				pitch = tempPitch;
				if (vel.z > 0.0)	bFLOORHUGGER = false;
			}
		}
		*/
		if (health == 1 && !(player.cheats & CF_BUDDHA2))
		{
			// The 'fake' death. In short, the player 'dies' technically, but
			// in reality they just become an invisible version of themselves
			// that cannot attack but can move through things like a ghost.
			// Based on how they were killed, they will respawn after X amount
			// of seconds.
			
			// Darwinians:		5-10
			// Monsters:		10-20
			// Soul Destroyer:	30+
			// Stupidity:		30+
			
			RespawnTimer = 35 * 5;
			SetDead();
			dead = true;
			
			Actor src = (source != null) ? source : inflictor;
			
			/*
			if (src)
			{
					 if (src == self || src.GetClass() == "SoulDestroyer")	RespawnTimer = 35 * 30;
				else if (src is "Darwinian" || src is "DarwinianPlayer")	RespawnTimer = 35 * 5;
				else 														RespawnTimer = 35 * 10;
			}
			*/
			
			DarwinianDie();
			
			if (!bNOPAIN)	
			{
				A_PlaySound("Darwinian/Hit",CHAN_BODY);
			}
			A_PlaySound("Darwinian/Death",CHAN_VOICE);
			
		}
		else if (health < 1)
		{
			// The REAL death only happens if they lose the game. This means
			// no respawning.
			dead = true;
			if (!bNOPAIN)	
			{
				A_PlaySound("Darwinian/Hit",CHAN_BODY);
			}
			A_PlaySound("Darwinian/Death",CHAN_VOICE);
			SetPlayerProperty(0,true,PROP_TOTALLYFROZEN);
			return dmg;
		}
		else if (dmg > 0)
		{
			if (!bNOPAIN)	
			{
				A_PlaySound("Darwinian/Damage",CHAN_VOICE);
			}
			A_PlaySound("Darwinian/Hit",CHAN_BODY);
			
		}
		
		if (GameOver && !bKILLED)	Die(self, self, 0);
		return dmg;
	}
	//==========================================================================
	
	override void BeginPlay()
	{
		Super.BeginPlay();
		ChangeStatNum(STAT_DARWINIAN);
	}
	
	//==========================================================================
	//==========================================================================
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		Scale.X = 0.5 + frandom(-0.05, 0.05);
		Scale.Y = 0.5 + frandom(-0.05, 0.05);
		
		if (!player || !player.mo || player.mo != self)
			return Super.PostBeginPlay();
			
		if (!tid)
			ChangeTID(TID_Darwinian);
		
		
		//Entities[HitBox] = Spawn("DarwinianPlayerHitBox", pos);
		//Entities[ChaseCam] = Spawn("ChaseCam", pos);
		Entities[ThirdPerson] = Spawn("DarwinianThirdPerson", pos, NO_REPLACE);
		
		for (int i = 0; i < Entities.Size(); i++)
		{
			if (Entities[i] != null)
			{
				Entities[i].Angle = Angle;
				Entities[i].master = self;
				Entities[i].Scale = Scale;
				TeamManager.SetTeam(Entities[i], DesignatedTeam);
			}
		}
		
		let a1 = DWBase(Entities[HitBox]);
		if (a1)	a1.DesignatedTeam = DesignatedTeam;
		
		if (!SpawnAir)
		{
			A_Warp(DefPtr,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_TOFLOOR);
			bFLOORHUGGER = true;
			SetAlive();
		}
		TeamManager.SetTeam(self, DesignatedTeam);
		
	}
	//==========================================================================
	//==========================================================================
	override void Tick()
	{
		if (!player || !player.mo || player.mo != self || GameOver || health < 1)
			return Super.Tick();
			
		if (dead)
		{
			if (!GameOver)
			{
				if (RespawnTimer <= 0)
					SetAlive();
				else
					RespawnTimer--;
			}
			return Super.Tick();
		}
		/*
		if (GetCvar("debughax"))
		{
			int ip = GetPlayerInput(MODINPUT_BUTTONS);
			int iz = GetPlayerInput(MODINPUT_OLDBUTTONS);
			if (ip & BT_USER3 && !(iz & BT_USER3))
			{
				
				DesignatedTeam = Clamp((DesignatedTeam + 1) % 9, 1, 8);
				TeamManager.SetTeam(self, DesignatedTeam);
				A_LogInt(DesignatedTeam);
				
				if (Entities.Size() > 0)
				{
					for (int i = 0; i < Entities.Size(); i++)
					{
						if (Entities[i] != null)
							TeamManager.SetTeam(Entities[i], DesignatedTeam);
					}
				}
			}
		}
		*/
		
		if (player.cheats & (CF_NOCLIP|CF_NOCLIP2))
		{
		}
		else if (!bFLOORHUGGER)
		{
			speed = 5;
			bFLOORHUGGER = (A_CheckFloor("Spawn") && vel.z == 0);
		}
		
		// ALWAYS make sure players have buddha.
		bBUDDHA = true;
		invis = (player.cheats & CF_CHASECAM);
		A_SetRenderStyle(1.0,(player.cheats & CF_CHASECAM) ? STYLE_None : STYLE_Normal);		
		
		if (Entities.Size() > 0)
		{
			for (int i = 0; i < Entities.Size(); i++)
			{
				if (Entities[i] != null)
				{
					Entities[i].vel = vel;
					Entities[i].SetOrigin(pos,false);
				}
			}
		}
		Super.Tick();
		bNOGRAVITY = bFLOORHUGGER;
	}
	
	//==========================================================================
	//==========================================================================
	override void MovePlayer()
	{	
		let player = self.player;
		if (dead || player.cheats & (CF_NOCLIP|CF_NOCLIP2|CF_FLY))
		{
			speed = 1;
			bFLOORHUGGER = false;
			return Super.MovePlayer();
		}
		UserCmd cmd = player.cmd;

		// [RH] 180-degree turn overrides all other yaws
		if (player.turnticks)
		{
			player.turnticks--;
			Angle += (180. / TURN180_TICKS);
		}
		else
		{
			Angle += cmd.yaw * (360./65536.);
		}

		player.onground = (pos.z <= floorz) || bOnMobj || bMBFBouncer || (player.cheats & CF_NOCLIP2);
		
		//----------------------------------------------------------------------
		// Process speed handling.
		//----------------------------------------------------------------------
		if (bFLOORHUGGER)
		{
			speed = 5;
			let input = GetPlayerInput(MODINPUT_BUTTONS);
			
			bool forward = (input & BT_FORWARD);
			bool backward = (input & BT_BACK);
			bool left = (input & BT_MOVELEFT);
			bool right = (input & BT_MOVERIGHT);
			
			// Save some time by checking to see if they cancel each other out first.
			if ((forward != backward) || (left != right))
			{
				if (player.cheats & CF_REVERTPLEASE)
				{
					player.cheats &= ~CF_REVERTPLEASE;
					player.camera = player.mo;
				}
				double	baseVel = 		speed, // The speed to move by.
						pitchMod = 		1.0, // The multiplier based on moving up or down.
						forwardvel = 	0.0, // The end result for going forward or backward.
						sidevel = 		0.0; // The end result for strafing.

				if (Entities[ThirdPerson])
				{
					// Slow down if going up, speed up if going down.
					double tp = Entities[ThirdPerson].pitch + 90;
					pitchMod = (tp < 0.0) ? cos(tp) : 1 + sin(tp);
					pitchMod = Clamp(pitchMod, 0.2, 1.8);
				}
				if (forward)	forwardVel += baseVel;
				if (backward)	forwardVel -= baseVel;
				if (left)		sidevel += baseVel;
				if (right)		sidevel -= baseVel;
				
				if (forward || backward || left || right)
				{
					if (input & BT_SPEED)
					{
						forwardvel /= 2.0;
						sidevel /= 2.0;
					}
					// Sorry guys, no strafe running!
					if (forwardvel != 0.0 && sidevel != 0.0)
					{	forwardvel *= 0.7;	sidevel *= 0.7;	}
					A_ChangeVelocity(forwardvel * pitchMod, sidevel * pitchMod, vel.z, CVF_RELATIVE|CVF_REPLACE);
				}
			}
		}
		else
		{
			bFLOORHUGGER = player.onground;
		}

	}

	//==========================================================================
	// Set these to do nothing. They will never be needed.
	override void PlayAttacking() { }
	override void PlayAttacking2() { }
	override void PlayRunning() { }
	override void PlayIdle() { }
	//==========================================================================
	
	override bool CanCollideWith(Actor other, bool passive)
	{
		if (!passive)
		{
			if (!other) return false;
			
			if (other is "DWBuilding")
			{
				return (Distance2D(other) <= other.Radius);
			}
			//return false;
		}
		return true;
	}
	
	
	//--------------------------------------------------------------------------
	// Spawns gibs and allows the player to float freely.
	//--------------------------------------------------------------------------
	void DarwinianDie()
	{
		
		double DSpeed = 0, oldAngle = angle, oldPitch = pitch;
		if (vel.x || vel.y || vel.z)
		{	
			A_FaceMovementDirection();
			DSpeed = vel.Length() * 2;
		}
		A_Stop();
		A_Scream();
		A_Fall();
		
		if (!Soulless)
		{
			if (DamageTypeReceived == 'SoulDestroyer')		SoulActor = "DarwinianGhost";
			else											SoulActor = "DigitalSoul";
				
			A_SpawnItemEx(SoulActor,0,0,height/2.0,cos(pitch) * DSpeed * 0.25, 0, -sin(pitch) * DSpeed * 0.25, 0, 
				SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		}
			
		int rng = random(2,5);
		for (int i = 0; i < rng; i++)
		{
			double np = pitch + frandom(-20.0, 20.0);
			A_SpawnItemEx("DarwinianLimb",0,frandom(-4.0, 4.0),frandom(2.0, 5.5),
				cos(np) * DSpeed,0,-sin(np) * DSpeed,frandom(-20.0,20.0),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		}
		
		angle = oldAngle;
		pitch = oldPitch;
	}
	void SetDead()
	{
		dead = true;
		A_GiveInventory("PowerFlight",1);		
		bVULNERABLE = bFLOORHUGGER = bSHOOTABLE = false;
		bTHRUACTORS = bINVISIBLE = bNOCLIP = bNONSHOOTABLE = true;
		Speed = 1;
	}
	
	void SetAlive()
	{
		A_SetInventory("PowerFlight",0);
		A_ResetHealth();
		bSHOOTABLE = bFLOORHUGGER = bSOLID = true;
		bTHRUACTORS = bNONSHOOTABLE = bNOCLIP = bFLY = bNOGRAVITY = bINVISIBLE = false;
		A_Stop();
		SetOrigin(SpawnPoint, false);
		Speed = 5;
		Angle = SpawnAngle;
		dead = false;
	}
}